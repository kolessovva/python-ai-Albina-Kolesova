# Руководство по созданию SPARQL-запросов к Викиданным для визуализации в Python
# SPARQL Query Guide for Wikidata — Python Visualization Course

***

## Назначение файла

Этот файл — руководство по написанию SPARQL-запросов к Викиданным
для лабораторной работы 1 курса «Python + AI» (ПетрГУ, 2026).

Смежные файлы:
- task_2_requirements.txt — требования к ноутбуку (задание 2)
- task_3_ideas_visual.txt — идеи и правила для визуализации (задание 3)

***

## Часть A. Стиль SPARQL-запросов

### A1. Структура файлов

- Несколько простых файлов вместо монолита: volcano.sparql, mountain_range.sparql
- Жёсткая схема потока данных: .sparql → data/*.csv → анализ в Python/pandas
- Конвенция имён: sparql/[сущность].sparql → data/[сущность].csv;
  один файл — один тип объекта
- Расширение файла — строчными буквами: sparql/monuments.sparql,
  не sparql/monuments.SPARQL (единообразие между студентами)

Разделение файлов по типу данных:

| Файл                       | Что содержит                                          | Почему отдельно                              |
|----------------------------|-------------------------------------------------------|----------------------------------------------|
| entity_info.sparql         | Координаты, страна, год основания, числовые атрибуты  | Быстрый запрос, одна строка на объект        |
| entity_fin.sparql          | Выручка, прибыль, сотрудники по годам                 | Несколько строк на объект, нужен временной ряд |
| entity_events.sparql       | Даты событий через p:/pq:P585                         | Риск timeout, тяжёлый запрос                 |
| entity_relations.sparql    | Связи между объектами для графа                       | Потенциально большое количество рёбер        |
| entity_architects.sparql   | Архитекторы объектов (P84): пол, даты,                | Один объект — несколько архитекторов;        |
|                            | страна рождения/смерти, вуз                           | тяжёлый join                                 |
| entity_founders.sparql     | Основатели объектов (P112): те же поля                | Разная эпоха и тип данных, чем архитекторы   |
| entity_genealogy.sparql    | Родители объекта (P22 отец, P25 мать):                | Один объект — несколько родителей;           |
|                            | имя родителя, год рождения, порода                    | нужен FILTER(BOUND(?father) || BOUND(?mother)) |
| entity_visitors.sparql     | Посещаемость по годам (P1174 + P585),                 | Один объект — несколько строк;               |
|                            | подписчики по годам (P8687 + P585)                    | нужен временной ряд; в основном файле        |
|                            |                                                       | эти поля не хранить — теряется история.      |
| entity_lineage.sparql      | Технологическая родословная (P144 «based on»):        | Направленный граф наследования;              |
|                            | объект → на основе чего создан                        | один объект — несколько предков              |

Правило выбора между architects и founders: запустить пробный запрос для обоих
свойств и сравнить объём результатов. Создавать отдельный файл по тому свойству,
у которого существенно больше данных. Если оба дают достаточно — создавать оба.

Что даёт расширение запроса вторым SPARQL-файлом (join):

Классы визуализаций, появляющиеся только при join:
- Сетевые графы и двудольные графы (страна → объект → страна архитектора)
- Sankey-диаграммы исторических потоков
- Миграционные карты (страна рождения → страна смерти)
- Тройные связки (страна → объект → человек → страна рождения)

Что остаётся недоступным без второго датасета:
- Рёбра в сетевом графе (нет второй сущности — нет связей)
- Поколения в родословной
- Исторические потоки и географические паттерны

Вывод: если студент не строит второй датасет, он должен компенсировать
сложность другими методами — выборки по подвыборкам, лог-шкала, работа
с выбросами, error bars (см. task_3_ideas_visual.txt, раздел «Стандарты
выполнения визуализации»).

Пример запроса типа genealogy — horses_genealogy.sparql (лошади):

  # Horse genealogy: father (P22) and mother (P25)
  # Родословная лошадей: отец и мать
  SELECT
    ?horse ?horseLabel
    (YEAR(?birthDate) AS ?birthYear)
    ?father ?fatherLabel
    ?mother ?motherLabel
    ?breedLabel
  WHERE {
    ?horse wdt:P31 wd:Q726 ;        # экземпляр: лошадь
           wdt:P4743 ?breed ;        # порода
           wdt:P569 ?birthDate .     # дата рождения
    OPTIONAL { ?horse wdt:P22 ?father . }  # отец
    OPTIONAL { ?horse wdt:P25 ?mother . }  # мать
    FILTER(BOUND(?father) || BOUND(?mother))  # хотя бы один родитель известен
    SERVICE wikibase:label { bd:serviceParam wikibase:language "ru,en,mul" . }
  }

Результат сохраняется как data/horses_genealogy.csv.
Для других тематик заменить wdt:P31 wd:Q726 на нужный класс объекта,
а wdt:P4743 на актуальное связующее свойство.

- LIMIT — только при отладке; в финальном файле не используется

***

### A2. Шапка файла

Каждый .sparql-файл начинается с двух строк описания — на русском и английском:

  # Volcanoes: elevation, country, coordinates, mountain range
  # Вулканы: высота, страна, координаты, горная система
  SELECT ?volcano ?volcanoLabel ...

Порядок языков в шапке — на усмотрение автора.
Важно, что оба языка присутствуют: файлы используются в международном
репозитории, и краткое описание на каждом из языков делает их понятными
любому участнику курса.

- Директивы #defaultView... не используются:
  визуализация выполняется в Python/Colab, а не в SPARQL-редакторе

***

### A3. Комментарии в коде

- Комментарий к каждому тройному паттерну — в конце строки, кратко, по-русски
- Описание нестандартных конструкций (COALESCE, BIND, psv) — обязательно
- Блок комментариев над строкой — не писать; только inline

Правильно:
  ?volcano wdt:P31  wd:Q8072 .           # экземпляр: вулкан
  ?volcano wdt:P625 ?coord .             # координаты
  OPTIONAL { ?stmt pq:P585 ?date }       # квалификатор: момент времени
  BIND(YEAR(?date) AS ?year)             # только год, без полной даты

***

### A4. Переменные в SELECT

- URI главного объекта (?volcano, ?monument, ?company) — всегда в SELECT:
  нужен для отладки в задании 2 (клик в браузере) и для join между CSV-файлами

- Для объектов, по которым будет join между файлами:
  брать и URI, и Label (?mountainRange + ?mountainRangeLabel)

- Для уникальных категориальных полей (страна, тип объекта, стиль):
  достаточно только Label — URI избыточен (?countryLabel без ?country)

- Для города: Label обычно достаточно, омонимы редки;
  URI города добавлять только если в теме много городов с одинаковыми названиями

- Имена переменных — короткие и понятные: ?stmt, ?hq, ?coord, ?year;
  избегать ?theVolcanoEntity, ?value1

***

### A5. Префиксы

| Префикс               | Назначение                    | Пример                       |
|-----------------------|-------------------------------|------------------------------|
| wd:Q...               | Сущность                      | wd:Q8072 (вулкан)            |
| wdt:P...              | Прямое значение свойства      | wdt:P2044 (высота)           |
| p:P...                | Ссылка на заявление           | p:P2139                      |
| ps:P...               | Значение заявления            | ps:P2139 (выручка)           |
| pq:P...               | Квалификатор заявления        | pq:P585 (дата)               |
| psv:P...              | Структурированное значение    | psv:P2139 (для валюты)       |
| wikibase:quantityUnit | Единица измерения             | → валюта выручки             |

Важно: цепочка wdt:P.../wdt:P... из двух wdt: не даёт доступа к квалификаторам.
Для квалификаторов — только p:/pq:-паттерн.

***

### A6. SERVICE wikibase:label

- Всегда в конце блока WHERE, перед закрывающей }
- Рекомендуемый порядок языков: "ru,en,mul"
  Механизм fallback:
  1. ru  — возвращается русская метка, если она есть
  2. en  — если русской нет, возвращается английская
  3. mul — специальный код «multiple languages»: возвращается метка,
           помеченная в Викиданных как языконезависимая — собственные имена,
           торговые марки, латинские термины, одинаковые во всех языках.
  Если ни одна из меток не найдена — возвращается голый QID объекта.
  Не использовать "ru" в одиночку: при отсутствии русской метки
  поле останется пустым вместо английского варианта.
- Все Label-переменные формируются автоматически: ?volcanoLabel для ?volcano
- Не использовать rdfs:label + FILTER(LANG()...) — избыточно и сужает выборку

  SERVICE wikibase:label { bd:serviceParam wikibase:language "ru,en,mul" }
}

***

### A7. OPTIONAL и BIND

Правило: всё, что вычисляется из переменной внутри OPTIONAL,
остаётся внутри того же OPTIONAL.

Порядок вложенности: ps: (значение) → pq: (квалификатор)
  → psv:/quantityUnit (единица) → BIND

Правильно:
  OPTIONAL {
    ?company p:P2139 ?stmt .
    ?stmt ps:P2139 ?revenue .              # значение выручки
    OPTIONAL { ?stmt pq:P585 ?date }       # квалификатор: момент времени
    OPTIONAL {
      ?stmt psv:P2139 ?revVal .
      ?revVal wikibase:quantityUnit ?currency .  # валюта через psv
    }
    BIND(YEAR(?date) AS ?year)             # год — внутри OPTIONAL!
  }

Неправильно:
  OPTIONAL { ?stmt pq:P585 ?date }
  BIND(YEAR(?date) AS ?year)              # ОШИБКА: date может быть unbound

Диагностика: если OPTIONAL-поле (например, dissolvedYear) остаётся пустым
в CSV, хотя данные в Викиданных есть — первое, что проверить: не стоит ли
BIND снаружи OPTIONAL. Это самая частая причина «пропажи» вычисляемых полей.
Задайте Mistral вопрос: «почему BIND снаружи OPTIONAL не работает, и как
правильно переписать?»

***

### A8. UNION и VALUES

- VALUES — вместо длинного UNION для однородных типов:
    VALUES ?type { wd:Q835714  wd:Q41162  wd:Q183342 }
    ?subject wdt:P31 ?type .

- При UNION для разных продуктов — обязательно явная переменная типа (?productType)

- wdt:P31/wdt:P279* — ценная конструкция: захватывает все подклассы и существенно
  увеличивает число результатов; чем больше строк в CSV, тем богаче потенциал
  для визуализации. Сохранять по умолчанию.
  Отказываться от подклассов только если возникает реальный timeout.

Особый случай для астрономических данных: P361 («часть») и P59 («созвездие») —
разные свойства.
- P59 — принадлежность звезды к созвездию по классификации МАС. Именно его
  использовать для получения 88 классических созвездий.
- P361 — связывает звезду с любым объектом, частью которого она является
  (скопление, галактика, туманность). Даёт неверные результаты при поиске
  созвездий.

***

### A9. Фильтрация

| Конструкция               | Когда использовать                          |
|---------------------------|---------------------------------------------|
| FILTER(?a != ?b)          | Сравнение значений                          |
| FILTER(LANG(?l) = "ru")   | Только при строгой необходимости            |
| FILTER(?n > 10)           | Числовые пороги                             |
| FILTER NOT EXISTS { ... } | Исключение по наличию паттерна              |
| MINUS { ... }             | Вычитание множества результатов             |

FILTER NOT EXISTS предпочтительнее MINUS, если переменные из блока
используются в основном паттерне.

***

### A10. Агрегации и сортировка

- GROUP BY — включать все неагрегированные переменные из SELECT
- DISTINCT — только при реальных структурных дублях,
  не для маскировки ошибок join
- Подзапрос SELECT ... WHERE { SELECT ... } — для двухуровневых агрегаций

***

### A11. Координаты

Для простых объектов — напрямую:
  ?volcano wdt:P625 ?coord .             # координаты вулкана

Для объектов со штаб-квартирой — всё внутри одного OPTIONAL:
  OPTIONAL {
    ?company p:P159 ?hqStmt .
    ?hqStmt ps:P159 ?hq .
    OPTIONAL { ?hqStmt pq:P625 ?coordHQ }
    OPTIONAL { ?hq wdt:P625 ?coordCity }
    BIND(COALESCE(?coordHQ, ?coordCity) AS ?coord)
  }

Трёхзвенная цепочка через производителя (для технических объектов):
  Если объект → производитель (P176) → штаб-квартира (P159) →
  координаты (P625), использовать двойной OPTIONAL:
  OPTIONAL {
    ?object wdt:P176 ?manufacturer .
    OPTIONAL {
      ?manufacturer wdt:P159 ?hq .
      OPTIONAL { ?hq wdt:P625 ?coordMfr }
    }
  }
  Это позволяет строить карты «где сделан объект», даже если
  у самого объекта нет P625.

***

### A12. Финансовые свойства

- Никогда не использовать P1813 (short name) — даёт дубли
- Валюту — только через psv:P.../wikibase:quantityUnit
- Год — только через YEAR(?date), не полную дату

***

### A13. Оптимизация и риски

- Валюту и квалификаторы — всегда в OPTIONAL
- wdt:P31/wdt:P279* — осторожно на очень широких классах: риск timeout
- Тяжёлые конструкции (даты событий, временные ряды) — в отдельный файл
- Любой P-код, предложенный AI, проверять вручную на wikidata.org/entity/P...

Особый риск при VALUES + wdt:P31 ?type: если у объекта в Викиданных
несколько значений P31, и несколько из них попадают в список VALUES,
объект окажется в результате несколько раз — по одному разу на каждый
подходящий тип. Это не ошибка данных: объект действительно помечен
двумя типами одновременно (например, «красный сверхгигант» + «переменная
звезда»). Решение: добавить SELECT DISTINCT. Документировать число таких
объектов в ноутбуке задания 2.

***

## Часть B. Проектирование данных для визуализации

### B1. Общий принцип: от задачи к запросу

Прежде чем писать SPARQL, ответить на вопрос:
«Какую визуализацию я хочу получить на выходе?»
Тип визуализации определяет, какие свойства нужно извлечь и в каком виде.

***

### B2. Тип визуализации → данные → свойства

| Тип визуализации           | Нужный тип данных            | Типичные свойства                          |
|----------------------------|------------------------------|--------------------------------------------|
| Карта точек                | Координаты                   | wdt:P625 ?coord                            |
| Карта стран (хороплет)     | Label страны + числовой      | ?countryLabel + wdt:P1082 ?population      |
| Сетевой граф               | Пары URI: объект → связанный | wdt:P4552 ?mountainRange, wdt:P361 ?partOf |
| Временной ряд              | Даты событий                 | pq:P585 ?date → YEAR(?date) AS ?year       |
| Гистограмма / bar chart    | Числа                        | wdt:P2044 ?elevation, wdt:P2043 ?length    |
| Scatter plot               | Два числовых поля            | ?elevation + второе число                  |
| Генеалогическое дерево     | URI объекта + URI родителей  | wdt:P22 ?father, wdt:P25 ?mother           |
| Подписи и tooltip          | Читаемое название            | SERVICE wikibase:label "ru,en,mul"         |

***

### B3. Правило кратности строк

Свойства, дающие несколько строк на одну сущность, включать осознанно:

| Ситуация                                                        | Что делать в pandas              |
|-----------------------------------------------------------------|----------------------------------|
| Объект принадлежит нескольким странам (P17)                     | groupby + agg(list) или explode  |
| Объект имеет несколько значений числа                           | взять max через groupby          |
| Объект имеет данные за несколько лет                            | временной ряд — в отдельный файл |
| Объект имеет несколько соседей (P47), архитекторов (P84),       | отдельный файл                   |
| жанров (P136) или любое другое многозначное поле                | entity_relations.sparql          |
|                                                                 | или groupby + agg(list)          |

Числовые значения (высота, длина, численность) брать через wdt: —
уже в стандартных единицах без разбора квалификаторов.

Практический ориентир: если итоговый CSV содержит в несколько раз больше
строк, чем ожидается объектов (например, 527 строк при 88 созвездиях или
3228 строк при ~500 музеях) — в запросе есть многозначное поле. Это не
ошибка, но требует осознанного решения: либо вынести это поле в отдельный
файл entity_relations.sparql, либо запланировать groupby на шаге 4 задания 2
и задокументировать это.

***

### B4. Пример проектирования «от задачи»

Задача: построить карту вулканов с высотой и горной системой.

1. Карта → нужны координаты → wdt:P625 ?coord
2. Подписи → ?volcanoLabel, ?mountainRangeLabel
3. Горная система для раскраски точек → нужен URI для join →
   ?mountainRange + ?mountainRangeLabel
4. Высота для размера точки → число → wdt:P2044 ?elevation
5. Страна — только для tooltip → только Label → ?countryLabel

Итог: один файл volcano.sparql, одна строка на вулкан.

***

### B5. Думать вперёд: задание 2 как фильтр перед финализацией запроса

Перед финализацией списка свойств продумать три вопроса:
(а) Достаточно ли будет заполнено это поле, чтобы строить по нему графики?
(б) Даёт ли оно числовой или категориальный признак,
    пригодный для .describe() или .value_counts()?
(в) Можно ли по нему построить join с другим CSV-файлом?

Поле, которое не отвечает ни на один из этих вопросов, —
кандидат на исключение из запроса ещё до создания CSV.

Разреженные поля (заполнены у 10–15% объектов) не отбрасывать:
они часто несут самый интересный сигнал и становятся основой
нетривиальных визуализаций в задании 3.

Пробный COUNT для оценки заполненности OPTIONAL-поля:
Для каждого нового OPTIONAL-поля перед финализацией сделать пробный
запрос вида:
  SELECT (COUNT(?field) AS ?n) WHERE {
    ?subject wdt:P31 wd:QXXXX .
    OPTIONAL { ?subject wdt:PYYY ?field }
  }
и сравнить ?n с общим числом объектов. Поле с заполненностью менее 5% —
кандидат на исключение или на отдельное нарративное исследование
(«почему данных так мало?»).

***

## Чеклист SPARQL-файла (финальная проверка)

[ ] Шапка файла: первые две строки содержат описание на русском и английском
[ ] Используется SERVICE wikibase:label вместо rdfs:label + FILTER(LANG()...)
[ ] SERVICE wikibase:label стоит в конце блока WHERE, перед закрывающей }
[ ] Язык в SERVICE: "ru,en,mul", не одиночный "ru"
[ ] LIMIT отсутствует в финальном файле (LIMIT — только при отладке)
[ ] URI главного объекта есть в SELECT (для отладки в задании 2 и join между файлами)
[ ] Для страны, типа объекта и других уникальных категорий — достаточно Label,
    URI этих объектов в SELECT не нужен
[ ] Комментарии стоят inline (в конце строки), а не отдельным блоком над строкой
[ ] Имя файла в нижнем регистре: sparql/entity.sparql, не sparql/entity.SPARQL
[ ] BIND стоит внутри того же OPTIONAL, что и переменная, из которой он вычисляет
[ ] Нет обязательных (не-OPTIONAL) числовых полей, хорошо заполненных только
    в одной стране/регионе: если одна страна занимает >50% записей — проверить,
    все ли числовые поля стоят в OPTIONAL
[ ] Для связи звезда → созвездие использован P59, а не P361
    (P361 даёт скопления и галактики, а не созвездия МАС)
[ ] При использовании VALUES + wdt:P31 ?type — добавлен SELECT DISTINCT
    во избежание дублей от объектов с несколькими значениями P31
[ ] Если запрос содержит P47 (соседи), P84 (архитекторы), P136 (жанры) или
    другие потенциально многозначные OPTIONAL — задокументировано, что CSV
    будет в «длинном формате» (несколько строк на объект)
[ ] Все подписи, текстовые ячейки и ячейка Summary соответствуют теме студента,
    а не шаблону
